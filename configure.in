# Require autoconf 2.53
AC_PREREQ(2.53)

AC_INIT(globalplatform, 6.0.0, k_o_@users.sourceforge.net)
AC_COPYRIGHT(COPYING)
AM_INIT_AUTOMAKE([-Wall])
AC_CONFIG_MACRO_DIR([m4])

# fails on Ubuntu Dapper
# PKG_CHECK_MODULES([CHECK], [check >= 0.9.2], [AC_MSG_WARN([Check version too low, checks may not work properly])])

# check programs
AC_PROG_CC
m4_ifdef([AM_PROG_AR], [AM_PROG_AR])
AC_PROG_CPP
AC_PROG_INSTALL

# get host
AC_CANONICAL_HOST

case "$host" in
*-*-cygwin*)
	AC_LIBTOOL_WIN32_DLL
	AM_LIBTOOLFLAGS="$AM_LIBTOOLFLAGS -no-undefined"
  ;;
*-*-darwin*)
        AC_DEFINE(MACOSX)
	LDFLAGS+="-framework PCSC "
  ;;
*)
  ;;
esac

AC_PROG_LIBTOOL
AM_PROG_CC_C_O

# check header files
AC_HEADER_STDC
AC_CHECK_HEADER(stdio.h)

# check library functions
AC_FUNC_MALLOC
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(memcpy vsyslog, [], [AC_MSG_ERROR([Necessary function is not supported.])])

AX_CFLAGS_WARN_ALL

####################
#Debug Configuration
####################

AC_ARG_ENABLE(debug,
[  --enable-debug          enable debug messages.],
[ case "${enableval}" in
  yes)
    debug=true ;;
  no)
    debug=false ;;
  *) AC_MSG_ERROR([bad value ${enableval} for --enable-debug]) ;;
 esac], [debug=false])

if test x${debug} = xtrue ; then
  AC_DEFINE(DEBUG, 1, [enable full debug messaging])
fi


###################
#zlib Configuration
###################

dnl Check for zlib headers
AC_CHECK_HEADERS(zconf.h zlib.h, [],
        [AC_MSG_ERROR([zlib headers not found, install zlib
1.2.3 or later, or use CFLAGS=... ./configure])])

AC_CHECK_LIB(z, gzopen, [],
        [AC_MSG_ERROR([gzopen() not
found, install zlib 1.2.3 or later])])


######################
#OpenSSL Configuration
######################

dnl Check for OpenSSL headers
AC_CHECK_HEADERS(openssl/rand.h openssl/err.h openssl/evp.h openssl/pem.h, [],
        [AC_MSG_ERROR([OpenSSL headers not found, install OpenSSL
0.9.7e or later, or use CFLAGS=... ./configure])])

AC_CHECK_LIB(crypto, PEM_read_PrivateKey, [],
        [AC_MSG_ERROR([PEM_read_PrivateKey() not
found, install OpenSSL 0.9.7e or later])])

##################################################################
#Fix incorrect Makefile generation: undefined reference to `dlsym'
##################################################################
LDFLAGS+=" -ldl "

###############
#Output results
###############

AC_MSG_RESULT([enable debug     : $debug])

AC_OUTPUT([
Makefile
src/Makefile
src/globalplatform.pc
])

